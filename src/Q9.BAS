10 ON BREAK CONT
20 ON ERROR GOTO 240
30 |BANKOPEN,0:GOTO 330
40 ON ERROR GOTO 270
50 sa=0:SYMBOL AFTER 256
60 ON ERROR GOTO 0
70 mcentry=HIMEM-15:MEMORY mcentry-1
80 FOR p=mcentry TO mcentry+15
90 READ p$:POKE p,VAL("&"+p$)
100 NEXT
110 a%=0:CALL mcentry,@a%
120 MEMORY mcentry+15
130 version=(a%\ 256)+256*(a%MOD256)
140 IF version<&102 THEN 320
150 mcentry=HIMEM-&524
160 ON ERROR GOTO 290
170 IF mcentry<=&7FFF THEN ERROR 7
180 MEMORY mcentry-1:LOAD "bankman.bin",mcentry
190 CALL mcentry
200 |BANKOPEN,0
210 IF sa=0 THEN SYMBOL AFTER 240
220 GOTO 330
230 DATA E,FF,CD,15,B9,EB,DD,66,1,DD,6E,0,73,23,72,C9
240 ' first |bankopen error
250 IF ERR=28 AND ERL=30 THEN RESUME 40
260 GOTO 320
270 ' catch bad SYMBOL AFTER command
280 sa=1:RESUME NEXT
290 ' error in main program
300 CLOSEIN:MEMORY mcentry+&524
310 IF sa=0 THEN SYMBOL AFTER 240
320 PRINT "Load Failed"
330 CLS:MODE 0:LOCATE 7,7:PRINT"Q R A M ":RUN"QRAM"
